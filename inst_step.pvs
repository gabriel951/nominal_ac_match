% Authors:
%		Gabriel Ferreira Silva (*)
% 		Mauricio Ayala-Rincón (*)
% 		Daniele Nantes Sobrinho (*)
% 		Maribel Fernandez (**)
%		(*) - University of Brasília			(**) - King's College London
%
%%%%%%%%%%%%%%%%%%%%%%%

inst_step: THEORY
BEGIN
	IMPORTING unification, fresh_subs, diophantine, list_theory[number], list_theory[args], list_theory[basic_sub]

sigma, delta, sigma2, gamma, gamma1: VAR sub
rho: VAR renaming

X, Y: VAR variable

G, G1, Delta: VAR fresh_context 
pi: VAR perm



f, sym: VAR string

t, s, t1, t2, tsigma: VAR term
args, args1: VAR args
lst_args, lst_args1, lst_args2: VAR list[args]

unif_pair, unif_pair2: VAR unif_pair

% P stands for "unification Problem". Pu stands for "Unseen unification Problem".
% Ps stands for "Seen unification Problem". 
P, P1, P2, Ps, Pu: VAR unif_prb
lst_P, lst_P1, lst_P2: VAR list[unif_prb]

V, PV: VAR finite_set[variable]

% check if either one of the terms "s" or "t" are variables and whether can solve {s =? t} by instantiating.
% returns
%   1 - the new context we must work with 
%   1 - the substitution "sigma" computed (if a instantiation happened )
%   2 - a boolean "skip" indicating that the terms can't be unified via a instantiation of s
%   3 - a boolean "impossible" indicating that the terms can't be unified
inst_step(PV, G, t, s): [fresh_context, sub, bool, bool] =  
   IF var?(s) THEN
        IF (NOT member(V(s), vars(t))) AND (NOT member(V(s), PV)) THEN LET
	    % instantiate s to t
	    sigma = cons((V(s), ext(reverse(p(s)))(t)), null),
	    (G1, bool1) = fresh_subs?(sigma, G), 
	    skip = FALSE IN 
	    IF bool1 THEN (append(G1, G), sigma, skip, FALSE)
	    ELSE (G, null, skip, TRUE)
	    ENDIF 

	ELSIF var?(t) AND (NOT member(V(t), vars(s))) AND (NOT member(V(t), PV))  THEN LET
	    %instantiate t to s 
	    sigma = cons((V(t), ext(reverse(p(t)))(s)), null),
	    (G1, bool1) = fresh_subs?(sigma, G), 
	    skip = FALSE IN 
	    IF bool1 THEN (append(G1, G), sigma, skip, FALSE)
	    ELSE (G, null, skip, TRUE)
	    ENDIF 

	ELSIF var?(t) AND V(s) = V(t) THEN LET 
	    % since we are in matching, we solve the "fixpoint equation" by enlarging the context
	    G1 = append(fc(DS(p(t), p(s)), V(t)), G), 
	    sigma = null, skip = FALSE, impossible = FALSE IN 
	    (G1, sigma, skip, impossible) 

	% no solution
	ELSE LET sigma = null, skip = FALSE, impossible = TRUE IN 
	    	 (G, sigma, skip, impossible)
	ENDIF
	

   ELSIF var?(t) THEN
        IF (NOT member(V(t), PV)) AND (NOT member(V(t), vars(s))) THEN LET 
	    %instantiate t to s 
	    sigma = cons((V(t), ext(reverse(p(t)))(s)), null),
	    (G1, bool1) = fresh_subs?(sigma, G), 
	    skip = FALSE IN 
	    IF bool1 THEN (append(G1, G), sigma, skip, FALSE)
	    ELSE (G, null, skip, TRUE)
	    ENDIF 

	ELSE LET sigma = null, skip = FALSE, impossible = TRUE IN 
	    	 (G, sigma, skip, impossible)
	ENDIF
       
   % none of them are variables, we skip  
   ELSE LET sigma = null, skip = TRUE, impossible = FALSE IN 
        (G, sigma, skip, impossible) 

   ENDIF

% this function is refered in our paper as "instStep"
% receives:
%       1 - the context Gamma (G) that we are working with
%	2 - list of unification problem we still have not tried to instantiate
%       3 - list of unification problem we already have tried to instantiate
% 	4 - the substitution "sigma" computed so far
%       5 - list of protected variables we cannot instantiate
% returns:
%       1 - 
%	2 - the unification problem after we make all the instantiations
% 	3 - the substitution computed
% 	4 - a boolean that is TRUE if one of the unification pairs
% 	    gave an impossible substitution
inst_step(PV, G, Pu, Ps, sigma): RECURSIVE [fresh_context, unif_prb, sub, bool] =
   IF null?(Pu) THEN (G, Ps, sigma, FALSE) 
   ELSE LET 
   	(t, s) = car(Pu), 
   	(G1, delta, skip, fail) = inst_step(PV, G, t, s) IN
	IF fail THEN (G, null, sigma, TRUE)
	ELSIF skip THEN  
	     inst_step(PV, G, cdr(Pu), cons((t, s), Ps), sigma)
	ELSE LET
	     new_Pu = apply_sub(delta, cdr(Pu)), 
	     new_Ps = apply_sub(delta, Ps), 
	     new_sigma = append(delta, sigma) IN
	     inst_step(PV, G1, new_Pu, new_Ps, new_sigma)
	ENDIF
   ENDIF
MEASURE length(Pu)

% extends "inst_step" to a list of unification problems
% if a unification problem fails when we try to instantiate it, it is removed from the list
inst_step(PV, G, lst_P): RECURSIVE list[[fresh_context, unif_prb, sub]] =
    IF null?(lst_P) THEN null
    ELSE LET
    	 P = car(lst_P),
    	 (G1, new_P, sigma, fail) = inst_step(PV, G, P, null, null) IN 
	 IF fail THEN inst_step(PV, G, cdr(lst_P))
	 ELSE cons((G1, new_P, sigma), inst_step(PV, G, cdr(lst_P)))
	 ENDIF
    ENDIF
MEASURE lst_P BY << 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% LEMMAS
% instantiate step
inst_step_skip: CONJECTURE
    LET skip = inst_step(PV, G, t, s)`3 IN
    skip IMPLIES (NOT var?(t) AND NOT var?(s))

inst_step_delta_null: CONJECTURE
    LET (G1, delta, skip, fail) = inst_step(PV, G, t, s) IN
    NOT skip AND NOT fail AND delta = null IMPLIES var?(t) AND t = s

inst_step_unif: CONJECTURE
    LET (G1, delta, skip, fail) = inst_step(PV, G, t, s) IN
    NOT skip and NOT fail IMPLIES subs(delta)(t) = subs(delta)(s)
    
inst_step_append: CONJECTURE
    inst_step(PV, G, append(lst_P1, lst_P2)) =
    append(inst_step(PV, G, lst_P1), inst_step(PV, G, lst_P2))

% TO DO: I am not sure how I will adapt this
%inst_step_opt: CONJECTURE
%    member((X, t1), inst_step(PV, G, t, s)`1) IMPLIES
%    (variable(X) = t AND t1 = s OR variable(X) = s AND t1 = t)
    
% TO DO: not sure how I will adapt this
%inst_step_dom: CONJECTURE
%    member(X, dom(inst_step(t, s)`1)) IMPLIES (variable(X) = t OR variable(X) = s)

inst_step_supset_dom_dom2: CONJECTURE
    member(X, supset_dom(inst_step(PV, G, t, s)`2)) IMPLIES member(X, dom(inst_step(PV, G, t, s)`2))

inst_step_dom_img: CONJECTURE
    LET sigma = inst_step(PV, G, t, s)`2, skip = inst_step(PV, G, t, s)`3,
    	fail = inst_step(PV, G, t, s)`4 IN
    NOT fail AND NOT skip AND NOT null?(sigma) IMPLIES
    (var?(s) AND dom(sigma) = singleton(V(s)) AND img(sigma) = singleton(t)) OR
    (NOT var?(s) AND var?(t) AND dom(sigma) = singleton(V(t)) AND img(sigma) = singleton(s))

inst_step_img: LEMMA
    subset?(vars(img(inst_step(PV, G, t, s)`2)), vars(t, s))

%inst_step_cont: CONJECTURE  
%    contained?(inst_step(PV, G, t, s)`2, vars(t, s))

inst_step_fail_ren: CONJECTURE 
   (FORALL X: 
      member(X, vars(img(rho))) AND NOT member(X, dom(rho)) IMPLIES NOT member(X, vars(t, s)))
   IMPLIES
   inst_step(PV, G, t, s)`4 = inst_step(PV, G, subs(rho)(t), subs(rho)(s))`4 AND
   inst_step(PV, G, t, s)`3 = inst_step(PV, G, subs(rho)(t), subs(rho)(s))`3 

inst_step_sub_ren: CONJECTURE   
   LET t_ = subs(rho)(t), s_ = subs(rho)(s), 
       sigma = inst_step(PV, G, t, s)`2,
       sigma_ = inst_step(PV, G, t_, s_)`2 IN 
   (FORALL X: 
      member(X, vars(img(rho))) AND NOT member(X, dom(rho)) IMPLIES NOT member(X, V))
   AND subset?(vars(t, s), V) IMPLIES 
   equal_syn?(append(sigma_, rho), append(rho, sigma), V)
 
inst_step_idempotent2: LEMMA
    idempotent?(inst_step(PV, G, t, s)`2)

inst_step_fail_null: CONJECTURE
    LET fail = inst_step(PV, G, P, Ps, sigma)`4
    IN fail AND NOT null?(P) IMPLIES inst_step(PV, G, P, Ps, sigma)`2 = null

inst_step_fail2: CONJECTURE
    LET fail = inst_step(PV, G, P, Ps, sigma)`4
    IN fail IMPLIES inst_step(PV, G, P, Ps, sigma)`2 = null

inst_step_idempotent: CONJECTURE
    LET delta = inst_step(PV, G, P, Ps, sigma)`3 IN
    idempotent?(sigma) AND disjoint?(vars(P), dom(sigma)) IMPLIES 
    idempotent?(delta)

inst_step3_no_effect: LEMMA
    inst_step(PV, G, P, P1, sigma)`3 =
    inst_step(PV, G, P, P2, sigma)`3

inst_step3_no_effect_cor: LEMMA
    inst_step(PV, G, P, P1, sigma)`3 =
    inst_step(PV, G, P, null, sigma)`3

inst_step_sub_append: LEMMA
    LET delta = inst_step(PV, G, P, Ps, sigma)`3,
        delta1 = inst_step(PV, G, P, Ps, null)`3 IN 
	delta = append(delta1, sigma)

inst_step_sub_append_cor: LEMMA 
    inst_step(PV, G, P, null, sigma)`3 =
    append(inst_step(PV, G, P, null, null)`3, sigma)


inst_step_sub: CONJECTURE
    member((G1, P1, sigma), inst_step(PV, G, lst_P)) IMPLIES
    idempotent?(sigma)  

inst_step4_no_effect: CONJECTURE
    inst_step(PV, G, P, P1, sigma)`4 =
    inst_step(PV, G, P, P2, sigma)`4

inst_step4_no_effect_cor: CONJECTURE 
    inst_step(PV, G, P, P1, sigma)`4 =
    inst_step(PV, G, P, null, sigma)`4
    
inst_step4_no_effect2: CONJECTURE
    inst_step(PV, G, P, P1, sigma)`4 =
    inst_step(PV, G, P, P1, sigma2)`4
    
inst_step4_no_effect2_cor: CONJECTURE 
    inst_step(PV, G, P, P1, sigma)`4 =
    inst_step(PV, G, P, P1, null)`4
    
inst_step2_sub: CONJECTURE
    inst_step(PV, G, P, Ps, append(sigma, sigma2))`2 =
    inst_step(PV, G, P, Ps, sigma2)`2 

inst_step2_sub2: CONJECTURE
    inst_step(PV, G, P, Ps, sigma)`2 =
    inst_step(PV, G, P, Ps, null)`2 

inst_step2_append: CONJECTURE
    LET delta = inst_step(PV, G, P, Ps, null)`3,
        fail = inst_step(PV, G, P, Ps, null)`4 IN 
    NOT fail IMPLIES
    inst_step(PV, G, P, Ps, null)`2 = 
    append(inst_step(PV, G, P, null, null)`2, apply_sub(delta, Ps))

inst_step2_mem: CONJECTURE
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    member(t, inst_step(PV, G, P, null, null)`2) IMPLIES 
    EXISTS s:
       NOT var?(s) AND member(s, P) AND t = subs(delta)(s)

inst_step1_mem_unif_pair: CONJECTURE 
    LET delta = inst_step(PV, G, P, Ps, null)`3 IN 
    member(unif_pair2, inst_step(PV, G, P, Ps, null)`2) IMPLIES 
    EXISTS unif_pair:
       member(unif_pair, append(P, Ps)) AND unif_pair2 = apply_sub(delta, unif_pair)

inst_step1_subset: CONJECTURE  
    LET (G1, P2, delta, fail) = inst_step(PV, G, P, null, null) IN 
    EXISTS P1:
       P2 = apply_sub(delta, P1) AND subset?(P1, P)

inst_step3_mem: CONJECTURE
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    member(t, img(delta)) AND NOT var?(t) IMPLIES 
    EXISTS s:
       NOT var?(s) AND member(s, P) AND t = subs(delta)(s)

% TO DO: this needs to be redone
%inst_step3_mem_var: CONJECTURE
%    LET delta = inst_step(PV, G, P, null, null)`3 IN
%    member(pi * X, img(delta)) IMPLIES member(pi * X, P)
    
inst_step_vars_img: LEMMA
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    subset?(vars(img(delta)), vars(P))

inst_step_idempotent3: LEMMA
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    idempotent?(delta)     

inst_step_mem: LEMMA
    member((G1, P, sigma), inst_step(PV, G, lst_P)) IMPLIES
    EXISTS P1:
	 member(P1, lst_P) AND 
	 G1 = inst_step(PV, G, P1, null, null)`1 AND
         P = inst_step(PV, G, P1, null, null)`2 AND
	 sigma = inst_step(PV, G, P1, null, null)`3 AND
	 NOT inst_step(PV, G, P1, null, null)`4
	 
inst_step_mem3: CONJECTURE  
    (EXISTS P1: 
	 member(P1, lst_P) AND 
	 G1 = inst_step(PV, G, P1, null, null)`1 AND 
         P = inst_step(PV, G, P1, null, null)`2 AND
	 sigma = inst_step(PV, G, P1, null, null)`3 AND
	 NOT inst_step(PV, G, P1, null, null)`4)
    IMPLIES member((G1, P, sigma), inst_step(PV, G, lst_P))

inst_step_no_var_pair: CONJECTURE
    P1 = inst_step(PV, G, P2, null, null)`2 AND no_pair?(P2)
    IMPLIES no_var_pair?(P1) 

inst_step_no_pair_img: CONJECTURE
     no_pair?(P) IMPLIES no_pair?(img(inst_step(PV, G, P, null, null)`3))

% TO DO: I don't think this will need to be adapted 
%inst_step_sub_im_under: CONJECTURE
%    LET delta = inst_step(PV, G, P, null, null)`3 IN
%    member(t, subterms(img(delta))) AND im_under?(X, t) AND no_pair?(P) IMPLIES
%    EXISTS Y, s:
%        member(s, subterms(P)) AND subs(delta)(Y) = variable(X)
%	AND im_under?(Y, s) AND same_func?(t, s)


% TO DO: not sure how I will adapt these
%inst_step_var_member: CONJECTURE
%     LET delta = inst_step(PV, G, P, null, null)`3 IN 
%     X /= Y AND subs(delta)(X) = variable(Y) IMPLIES
%     member(pi * X, P) AND member(pi * Y, P)

%inst_step_rhs_not_id: CONJECTURE
%    LET delta = inst_step(PV, G, P, null, null)`3,
%        fail = inst_step(PV, G, P, null, null)`4 IN 
%    NOT fail AND member(variable(X), rhs(P)) AND NOT member(variable(X), lhs(P))
%    IMPLIES subs(delta)(X) /= variable(X)

inst_step_sub_nice_aux: CONJECTURE
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    member((X, t), delta) IMPLIES member(pi * X, P) AND
    subset?(vars(t), vars(P))
    
inst_step_supset_dom_dom: CONJECTURE
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    member(X, supset_dom(delta)) IMPLIES member(X, dom(delta))

inst_step_sub_nice: CONJECTURE
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    nice?(delta) 
    
inst_step_dom2: CONJECTURE
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    member(X, dom(delta)) IMPLIES member(pi * X, P)

inst_step_sec_proj: CONJECTURE
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    member(pi * X, sec_proj(delta)) IMPLIES member(pi * X, P)


inst_step_cont: CONJECTURE 
    LET delta = inst_step(PV, G, P, null, null)`3 IN
    contained?(delta, vars(P))
    
%inst_step_not_temp_unif_pair: CONJECTURE
%    LET delta = inst_step(PV, G, P, null, null)`3 IN 
%    member(t, inst_step(P, null, null)`2)
%    IMPLIES EXISTS s, unif_pair:
%       NOT var?(s) AND t = subs(delta)(s) AND member(s, unif_pair) AND member(unif_pair, P) 
%       AND NOT temp?(unif_pair, P)
    
inst_step_get_args: CONJECTURE
    LET delta = inst_step(PV, G, P, null, null)`3 IN 
    member(t, img(delta)) AND member(s, get_args(f)(t)) IMPLIES
    EXISTS t1: 
       member(t1, P) AND
       EXISTS t2: 
          member(t2, get_args(f)(t1)) AND s = subs(delta)(t2) AND ord(t2) = ord(s) 
    
inst_step_unifies: CONJECTURE
    LET delta = inst_step(PV, G, P, null, null)`3,
        fail = inst_step(PV, G, P, null, null)`4 IN
    (EXISTS gamma: sigma = append(gamma, append(delta, gamma1)))
    AND unifies?(G, sigma, inst_step(PV, G, P, null, null)`2) AND NOT fail
    AND disjoint?(dom(gamma1), vars(P)) IMPLIES  
    unifies?(G, sigma, P)

inst_step_unifies_sub: CONJECTURE 
    LET delta = inst_step(PV, G, P, null, null)`3,
        fail = inst_step(PV, G, P, null, null)`4 IN
        unifies?(G, sigma, P) AND NOT fail
    IMPLIES equal?(G)(sigma, append(sigma, delta))

inst_step_unifies_not_fail: CONJECTURE 
    LET fail = inst_step(PV, G, P, null, null)`4 IN
        unifies?(G, sigma, P) AND well_formed?(P) IMPLIES NOT fail
    
inst_step_lst_mem: CONJECTURE 
    LET (G1, P1, delta, fail) = inst_step(PV, G, P, null, null) IN 
    unifies?(G, sigma, P) AND well_formed?(P) AND member(P, lst_P)
    IMPLIES member((G1, P1, delta), inst_step(PV, G, lst_P))

inst_step_well_formed: CONJECTURE 
    LET delta = inst_step(PV, G, P, null, null)`3 IN  
    well_formed?(P) IMPLIES well_formed?(delta)

inst_step_ren3: CONJECTURE  
   LET sigma = inst_step(PV, G, P, null, null)`3,
       sigma_ = inst_step(PV, G, apply_sub(rho, P), null, null)`3 IN 
   (FORALL X: 
      member(X, vars(img(rho))) AND NOT member(X, dom(rho)) IMPLIES NOT member(X, V))
   AND subset?(vars(P), V) IMPLIES 
   equal_syn?(append(sigma_, rho), append(rho, sigma), V)
     
inst_step_ren4: CONJECTURE
   (FORALL X: 
      member(X, vars(img(rho))) AND NOT member(X, dom(rho)) IMPLIES NOT member(X, V)) 
   AND subset?(vars(P), V) IMPLIES 
   inst_step(PV, G, P, null, null)`4 =
   inst_step(PV, G, apply_sub(rho, P), null, null)`4    

inst_step_ren2: CONJECTURE 
   (FORALL X: 
      member(X, vars(img(rho))) AND NOT member(X, dom(rho)) IMPLIES NOT member(X, vars(P)))
    IMPLIES
    inst_step(PV, G, apply_sub(rho, P), null, null)`2 =
    apply_sub(rho, inst_step(PV, G, P, null, null)`2)

END inst_step
