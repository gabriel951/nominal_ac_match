% Authors:
%		Gabriel Ferreira Silva (*)
% 		Mauricio Ayala-Rincón (*)
% 		Daniele Nantes Sobrinho (*)
% 		Maribel Fernandez (**)
%		(*) - University of Brasília			(**) - King's College London
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

rename_input: THEORY
BEGIN 
      %IMPORTING apply_ac, more_list_theory_props
      IMPORTING ac_step, more_list_theory_props

pi: VAR perm
X, Y: VAR variable
t, s: VAR term
f: VAR string 
n: VAR nat
args: VAR args

G, G_, G1, delta: VAR fresh_context

input, input_, input1, input1_, input2: VAR nice_input 
fixed_vars, FV, V, V_, V1, V1_, PV, PV_: VAR finite_set[variable]
lst_V, lst_V_: VAR list[variable]
rho: VAR renaming
dio: VAR dio_matrix
lst_args: VAR list[args]

sig, sig1, sig_, sig1_: VAR sub

P, P1, P2, P_, P1_, Pc, Pc_, Pu, Pu_, Pu1, Pu1_, Ps, Ps_, Ps1, Ps1_: VAR unif_prb

renamed_inputs?(input, input_, fixed_vars, rho): bool = 
   LET (G, unif_prb, sigma, V, PV) = input,
       (G_, unif_prb_, sigma_, V_, PV_) = input_ IN 
   unif_prb_ = apply_sub(rho, unif_prb) AND equal_syn?(sigma_, append(rho, sigma), fixed_vars)
   AND (select_greater[nat](V) <= select_greater[nat](V_)) AND subset?(fixed_vars, V)
   AND subset?(dom(rho), V) AND subset?(vars(img(rho)), V_) AND
   (FORALL X:
      member(X, vars(img(rho))) AND NOT member(X, dom(rho)) IMPLIES NOT member(X, V))
   AND PV = PV_  AND G_ = subs(rho, G) 

renamed_inputs?(input, input_, fixed_vars): bool =
   EXISTS rho: 
       renamed_inputs?(input, input_, fixed_vars, rho)


%%% LEMMAS
ren_input_null: CONJECTURE 
   renamed_inputs?(input, input_, fixed_vars) IMPLIES (null?(input`2) IFF null?(input_`2))

ren_input_choose: CONJECTURE  
   renamed_inputs?(input, input_, fixed_vars, rho) IMPLIES 
   choose_unif_pair(input_`2)`1 = apply_sub(rho, choose_unif_pair(input`2)`1) AND 
   choose_unif_pair(input_`2)`2 = apply_sub(rho, choose_unif_pair(input`2)`2)

% TO DO: this is probably wrong and will need to be adapted
ren_equal_aux: CONJECTURE  
   (FORALL X:
      member(X, vars(img(rho))) AND NOT member(X, dom(rho)) IMPLIES NOT member(X, V)) AND
    subset?(vars(t), V) AND subset?(vars(s), V) AND equal?(subs(rho, G))(subs(rho)(t), subs(rho)(s))
    IMPLIES equal?(G)(t, s)
    
ren_equal: CONJECTURE  
    renamed_inputs?(input, input_, fixed_vars, rho) AND
    member(t, input`2) AND member(s, input`2) AND 
    equal?(G)(subs(rho)(t), subs(rho)(s)) IMPLIES equal?(G)(t, s)

ren_input_append: CONJECTURE  
    nice_input?(G, append(Pu, Ps), sig, V, PV) AND nice_input?(G_, append(Pu_, Ps_), sig_, V_, PV_)
    AND length(Pu) = length(Pu_) IMPLIES 
    (renamed_inputs?((G, append(Pu, Ps), sig, V, PV), (G_, append(Pu_, Ps_), sig_, V_, PV_), fixed_vars, rho)
    IFF (renamed_inputs?((G, Ps, sig, V, PV), (G, Ps_, sig_, V_, PV_), fixed_vars, rho) AND
         renamed_inputs?((G, Pu, sig, V, PV), (G, Pu_, sig_, V_, PV_), fixed_vars, rho)))

renaming_mem_aux: CONJECTURE 
   (FORALL X:
      member(X, vars(img(rho))) AND NOT member(X, dom(rho)) IMPLIES NOT member(X, V)) AND 
    member(Y, V) AND subset?(vars(t), V) IMPLIES 
   (member(Y, vars(t)) IFF (var?(subs(rho)(Y)) AND member(V(subs(rho)(Y)), vars(subs(rho)(t)))))
    
% TO DO: this will need to be adapted 
renaming_mem: CONJECTURE 
    renamed_inputs?(input, input_, fixed_vars, rho) AND 
    member(pi * X, input`2) AND member(t, input`2) 
    IMPLIES
    (member(X, vars(t)) IFF (var?(subs(rho)(X)) AND member(V(subs(rho)(X)), vars(subs(rho)(t)))))

ren_input_inst: CONJECTURE  
    LET sig1 = cons((X, t), null), sig1_ = cons((V(subs(rho)(X)), subs(rho)(t)), null),
        (G, P, sig, V, PV) = input, (G_, P_, sig_, V_, PV_) = input_ IN 
    subset?(P1, P) AND P1_ = apply_sub(rho, P1) AND 
    NOT member(X, vars(t)) AND member(pi * X, P) AND member(t, P) AND 
    renamed_inputs?(input, input_, fixed_vars, rho)
    IMPLIES 
    renamed_inputs?((G, apply_sub(sig1, P1), append(sig1, sig), V, PV), 
                   (G_, apply_sub(sig1_, P1_), append(sig1_, sig_), V_, PV_), fixed_vars)

ren_input_choose2: CONJECTURE 
    LET (G, P, sig, V, PV) = input, (G_, P_, sig_, V_, PV_) = input_,
        P1 = choose_unif_pair(P)`2, P1_ = choose_unif_pair(P_)`2 IN 
    renamed_inputs?(input, input_, fixed_vars, rho) IMPLIES  
    renamed_inputs?((G, P1, sig, V, PV), (G_, P1_, sig_, V_, PV_), fixed_vars, rho)

ren_input_decompose: CONJECTURE    
    LET (G, P, sig, V, PV) = input, (G_, P_, sig_, V_, PV_) = input_,
        (up, P1) = choose_unif_pair(P), (up_, P1_) = choose_unif_pair(P_),
	(t, s) = up, (t_, s_) = up_ IN 
    (app?(t) AND app?(s) AND f_sym(t) = f_sym(s) AND
    app?(t_) AND app?(s_) AND f_sym(t_) = f_sym(s_) AND
    renamed_inputs?(input, input_, fixed_vars, rho)) AND NOT null?(P) IMPLIES 
    LET P2 = decompose(arg(t), arg(s))`1, P2_ = decompose(arg(t_), arg(s_))`1 IN 
    renamed_inputs?((G, append(P2, P1), sig, V, PV), (G_, append(P2_, P1_), sig_, V_, PV_), fixed_vars, rho)

ren_input_elim_com_arg: CONJECTURE  
    renamed_inputs?(input, input_, FV, rho) AND 
    member(t, input`2) AND member(s, input`2) IMPLIES 
    elim_com_arg(subs(rho, G), subs(rho)(t), subs(rho)(s), f)`1 = subs(rho)(elim_com_arg(G, t, s, f)`1) AND 
    elim_com_arg(subs(rho, G), subs(rho)(t), subs(rho)(s), f)`2 = elim_com_arg(G, t, s, f)`2 AND
    elim_com_arg(subs(rho, G), subs(rho)(t), subs(rho)(s), f)`3 = elim_com_arg(G, t, s, f)`3

ren_solve_ac_sec_proj_vars: CONJECTURE  
   LET V1 = vars(sec_proj(solve_ac(G, t, s, lst_V, f, PV))),
       V1_ = vars(sec_proj(solve_ac(subs(rho, G), subs(rho)(t), subs(rho)(s), lst_V_, f, PV))),
       Z = select_greater[nat](lst_V), Z1 = select_greater[nat](finset2list[nat](V1)),
       Z_ = select_greater[nat](lst_V_), Z1_ = select_greater[nat](finset2list[nat](V1_)) IN 
       NOT null?(solve_ac(G, t, s, lst_V, f, PV)) AND
       renamed_inputs?(input, input_, FV, rho) AND member(t, input`2) AND member(s, input`2)
       IMPLIES Z1 - Z + Z_ = Z1_ 

ren_get_pre_new_args: CONJECTURE   
    select_greater[nat](lst_V) <= select_greater[nat](lst_V_) AND n + 1 >= length(dio) IMPLIES
    LET rho = shift(lst_V, lst_V_, n) IN 
    get_pre_new_args(map(subs(rho))(lst_args), dio, lst_V_)`1 =
    map(subs(rho))(get_pre_new_args(lst_args, dio, lst_V)`1)

ren_dio_matrix2ac: CONJECTURE 
    LET rho1 = shift(lst_V, lst_V_, n) IN 
    (select_greater[nat](lst_V) <= select_greater[nat](lst_V_) AND 
    disjoint?(dom(rho1), vars(args)) AND subset?(dom(rho), V) AND
    subset?(vars(img(rho)), V_) AND n+1 >= length(dio)) AND
    list2set(lst_V) = V AND list2set(lst_V_) = V_ 
    IMPLIES 
    dio_matrix2ac_sol(subs(rho)(args), lst_V_, f)(dio)`1 = 
    apply_sub(append(rho, rho1), dio_matrix2ac_sol(args, lst_V, f)(dio)`1)

ren_input_vars_sec_proj: CONJECTURE 
    LET V1_ = vars(sec_proj(solve_ac(subs(rho, G), subs(rho)(t), subs(rho)(s), lst_V_, f, PV))),
    V1 = vars(sec_proj(solve_ac(G, t, s, lst_V, f, PV))) IN 
    select_greater[nat](lst_V) <= select_greater[nat](lst_V_) AND
    renamed_inputs?(input, input_, FV, rho) AND member(t, input`2) AND member(s, input`2)
    IMPLIES select_greater[nat](V1) <= select_greater[nat](V1_)

ren_input_solve_ac: CONJECTURE 
    LET V1_ = vars(sec_proj(solve_ac(subs(rho, G), subs(rho)(t), subs(rho)(s), lst_V_, f, PV))),
    V1 = vars(sec_proj(solve_ac(G, t, s, lst_V, f, PV))) IN 
    (nice_input?(G, append(Pu, Ps), sig, V, PV) AND nice_input?(G_, append(Pu_, Ps_), sig_, V_, PV_) AND 
    renamed_inputs?((G, append(Pu, Ps), sig, V, PV), (G_, append(Pu_, Ps_), sig_, V_, PV_), FV, rho) AND 
    length(Pu) = length(Pu_) AND NOT null?(Pu) AND t = car(Pu)`1 AND s = car(Pu)`2 AND 
    list2set(lst_V) = V AND list2set(lst_V_) = V_ AND are_ac?(t, s, f) AND 
    member(Pc_, first_proj(solve_ac(subs(rho, G), subs(rho)(t), subs(rho)(s), lst_V_, f, PV)))) IMPLIES 
    EXISTS Pc:
       member(Pc, first_proj(solve_ac(G, t, s, lst_V, f, PV))) AND 
       renamed_inputs?((G, append(cdr(Pu), append(Pc, Ps)), sig, V1, PV) ,
       		       (G_, append(cdr(Pu_), append(Pc_, Ps_)), sig_, V1_, PV_), FV)
       AND length(Pc) = length(Pc_)

ren_input_inst_step: CONJECTURE  
    LET (G, P, sig, V, PV) = input, (G_, P_, sig_, V_, PV_) = input_ IN 
    renamed_inputs?((G, P, sig, V, PV), (G_, P_, sig_, V_, PV_), FV, rho) AND
    subset?(P2, P) AND  
    sig1  = inst_step(PV, G, P2, null, null)`3 AND 
    sig1_ = inst_step(PV, subs(rho, G), apply_sub(rho, P2), null, null)`3 AND 
    subset?(P1, P) AND P1_ = apply_sub(rho, P1) IMPLIES 
    renamed_inputs?((G, apply_sub(sig1, P1), append(sig1, sig), V, PV),
    		    (subs(rho, G), apply_sub(sig1_, P1_), append(sig1_, sig_), V_, PV_), FV, rho)
		    
% use P2 = Pc and P1 as 1. Pu, 2. Ps 3. subset of Pc. 
ren_input_inst_step_cor: CONJECTURE  
    LET (G1, P1, sig1, fail) = inst_step(PV, G, Pc, null, null),
         sig1_ = inst_step(PV, subs(rho, G), apply_sub(rho, Pc), null, null)`3 IN
    nice_input?(G, append(Pu, append(Pc, Ps)), sig, V, PV) AND 
    nice_input?(G_, append(Pu_, append(Pc_, Ps_)), sig_, V_, PV_) AND 
    renamed_inputs?((G, append(Pu, append(Pc, Ps)), sig, V, PV),
                    (G_, append(Pu_, append(Pc_, Ps_)), sig_, V_, PV_), FV, rho)
    AND length(Pu) = length(Pu_) AND length(Pc) = length(Pc_) IMPLIES 
    renamed_inputs?((G, append(apply_sub(sig1, Pu), append(P1, apply_sub(sig1, Ps))), append(sig1, sig), V, PV),
                    (G_, append(apply_sub(sig1_, Pu_), append(apply_sub(rho, P1), apply_sub(sig1_, Ps_))),
		    			    	 append(sig1_, sig_), V_, PV_), FV, rho)

     
%ren_input_next_input: CONJECTURE 
%    nice_input?(append(Pu, Ps), sig, V) AND nice_input?(append(Pu_, Ps_), sig_, V_) AND 
%    renamed_inputs?((append(Pu, Ps), sig, V), (append(Pu_, Ps_), sig_, V_), FV) AND
%    next_input?((Pu_, Ps_, sig_, V_), (Pu1_, Ps1_, sig1_, V1_)) AND
%    member(input2, apply_ac_step((Pu1_, Ps1_, sig1_, V1_))) AND length(Pu) = length(Pu_)
%    IMPLIES 
%    EXISTS Pu1, Ps1, sig1, V1: 
%       next_input?((Pu, Ps, sig, V), (Pu1, Ps1, sig1, V1)) AND
%       renamed_inputs?((append(Pu1, Ps1), sig1, V1), (append(Pu1_, Ps1_), sig1_, V1_), FV) 
%       AND length(Pu1) = length(Pu1_)

% renamed inputs for the applyAC case 
%ren_input_apply_ac: CONJECTURE  
%    nice_input?(G, append(Pu, Ps), sig, V, PV) AND nice_input?(G_, append(Pu_, Ps_), sig_, V_, PV_)
%    AND renamed_inputs?((G, append(Pu, Ps), sig, V, PV), (G_, append(Pu_, Ps_), sig_, V_, PV_), FV) AND
%    member(input1_, apply_ac_step(G_, Pu_, Ps_, sig_, V_, PV_)) AND length(Pu) = length(Pu_)
%    IMPLIES EXISTS input1:
%     member(input1, apply_ac_step(G, Pu, Ps, sig, V, PV)) AND
%     renamed_inputs?(input1, input1_, FV)
    
%ren_input_apply_ac_cor: CONJECTURE  
%    renamed_inputs?(input, input_, FV) AND
%    member(input1_, apply_ac_step(input_`1, null, input_`2, input_`3))
%    IMPLIES EXISTS input1:
%       member(input1, apply_ac_step(input`1, null, input`2, input`3))
%       AND renamed_inputs?(input1, input1_, FV)



END rename_input
